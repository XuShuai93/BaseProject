//通过isBuildModule这个变量来确定每个模块是单独运行还是以Library的形式加载到整个App
if (rootProject.ext.isBuildModule) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply from: rootProject.file('gradle/common.gradle')


android {
    sourceSets {
        main {
            if (rootProject.ext.isBuildModule) {
                //在main文件夹下建立debug文件夹，下面可以放java文件，资源文件等测试文件
                //这里进行设置使用单独运行还是合并运行的Manifest.xml
                manifest.srcFile 'src/main/alone/AndroidManifest.xml'
                java.srcDirs = ['src/main/alone/java',
                                'src/main/java']
                res.srcDirs = ['src/main/alone/res',
                               'src/main/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java.srcDirs = ['src/main/java']
            }
        }
    }
}

dependencies {
    implementation project(path: ':common:library-base')
}